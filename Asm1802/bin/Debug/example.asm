..THIS PROGRAM IS NOT DESIGNED TO DO
..ANYTHING MEANINGFUL. RATHER, IT IS
..DESIGNED TO ACT AS A REVIEW AND
..SUMARY OF THE COSMAC LEVEL 1
..ASSEMBLY LANGUAGE
..A LINE MAY CONSIST ONLY OF A COMMENT
IDL;SAV;LDX         ..MULTIPLE STATEMENTS ON LINE
CRLF=#0D0A          ..SYMBOL DEFINED VIA EQUATE
TALLY=9             ..SAME USING DECIMAL CONSTANT
PTR=  TALLY + 1     ..EXPRESSION CONTAINS SYMBOL
LOOP: DEC TALLY     ..SYMBOL DEFINED VIA LABEL
      GLO TALLY     ..TALLY IDENTIFIES A REGISTER
      BNZ LOOP      ..EXPR CONTAINS SYMBOL ONLY
      BR SAM1       ..A FORWARD REFERENCE
,X'ABCD',64,D'256'  ..SOME CONSTANTS
DC B'011',T'TEXT'   ..MORE CONSTANTS
,A(* - 1),A.0(CRLF) ..SOME ADDRESS CONSTANTS
SAM1: INC A         ..REG NAMED WITH HEX DIGIT
      DEC RB        ..REG NAMED WITH "R" FORM
      OUT 7         ..EXPLICIT DEVICE NUMBER
      INP READER    ..DEVICE IDENTIFIER IS SYMBOL
READER = 7          ..ANOTHER FORWARD REFERENCE
      LDI T'A'      ..1-BYTE IMMEDIATE CONSTANT
      ADI NUMBER    ..SYMBOLIC IMMEDIATE CONSTANT
NUMBER=X'05'        ..EVEN NUMBER OF HEX DIGITS
      B3 LOOP-#03   ..BR ADDRESS IS EXPRESSION
      BN1 * + 2     ..EXPR REFERS TO LOC COUNTER
      ORG *+#10     ..ADVANCE LOC CTR 16 BYTES
      PAGE          ..ADV LOC CTR TO NEXT PAGE
      OUT 7,16      ..IF X = P, #10 IS DATA OUT
      LDA 2,NUMBER  ..IF P = 2, SAME AS LDI #5
F = 2 ..BAD PRACTICE TO REDEFINE A HEX CHAR SINCE
INC F ..THIS WILL INCREMENT R2, NOT RF.
LABEL:ORG *         ..AN EASILY MOVEABLE LABLE
RET:  SAV;          .."RET" IS A LABEL HERE
RET; SAV;           .."RET" IS AN INSTRUCTION
LDI A.1(LABEL)      ..A PROPER 1-BYTE IMMEDIATE
LDI A(LABEL)        ..ERROR, NO DIAGNOSTIC GIVEN
LDI LABEL           ..LOWER BYTE USED ONLY
INC CRLF            ..LOWEST HEX DIGIT USED ONLY
,CRLF               ..LOWEST BYTE USED ONLY
END                 ..REQUIERED LAST STATEMENT
